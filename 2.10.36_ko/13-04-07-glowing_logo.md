# 13.4.7. Glowing Logo

## 1. `íŒŒì¼` â†’ `ë§Œë“¤ê¸°` â†’ `ë¬´ëŠ¬` â†’ `Glowing Logo...`

#### ê·¸ë¦¼ 13.4.7.a101. ê²°ê³¼ ì´ë¯¸ì§€
![13-04-07-a101-result](https://github.com/wonder13662/gimp/assets/15767104/bf243586-706c-4989-b40d-a15f9133daa0)

#### ê·¸ë¦¼ 13.4.7.a102. ë©”ë‰´ ìœ„ì¹˜
![13-04-07-a102-menu](https://github.com/wonder13662/gimp/assets/15767104/df08747d-b03d-41d8-bcdb-3d9ee6f217cd)

#### ê·¸ë¦¼ 13.4.7.a103. ëŒ€í™”ìƒì
![13-04-07-a103-dialog](https://github.com/wonder13662/gimp/assets/15767104/ccac28b7-1a62-4737-bece-a0e887a4b4ec)

#### ì˜ìƒ 13.4.7.a104. ì „ì²´ ê³¼ì •
<video controls="controls" width="720" src="https://github.com/wonder13662/gimp/assets/15767104/bf0d0534-1b63-4977-8249-350d3d5161d3"></video>

## 2. `í•„í„°` â†’ `ì•ŒíŒŒë¥¼ ë¡œê³ ë¡œ` â†’ `Glowing Logo...`

#### ê·¸ë¦¼ 13.4.7.a111. ì›ë³¸ ì´ë¯¸ì§€
![13-04-07-a111-source](https://github.com/wonder13662/gimp/assets/15767104/f340af75-4a7a-452a-8400-1c2714b4080f)

#### ê·¸ë¦¼ 13.4.7.a112. ê²°ê³¼ ì´ë¯¸ì§€
![13-04-07-a112-result](https://github.com/wonder13662/gimp/assets/15767104/8f092da9-0d6c-4da0-ad24-49630cef5a23)

#### ê·¸ë¦¼ 13.4.7.a113. ë©”ë‰´ ìœ„ì¹˜
![13-04-07-a113-menu](https://github.com/wonder13662/gimp/assets/15767104/645c2da8-12af-4665-a0f0-0ef31ee5e286)

#### ê·¸ë¦¼ 13.4.7.a114. ëŒ€í™”ìƒì
![13-04-07-a114-dialog](https://github.com/wonder13662/gimp/assets/15767104/aa886b35-075c-4b4a-b377-a7c7e4780622)

#### ì˜ìƒ 13.4.7.a115. ì „ì²´ ê³¼ì •
<video controls="controls" width="720" src="https://github.com/wonder13662/gimp/assets/15767104/861c49d0-9ef0-4d66-8b65-5bc71bed9d10"></video>

## 3. ìŠ¤í¬ë¦½íŠ¸
ì´ ìŠ¤í¬ë¦½íŠ¸ì˜ ì½”ë“œëŠ” ì•„ë˜ì™€ ê°™ìŠµë‹ˆë‹¤. ìŠ¤í¬ë¦½íŠ¸ë¥¼ ìˆ˜ì •í•´ì„œ ì‚¬ìš©í•˜ê³  ì‹¶ë‹¤ë©´, [13.2. Script-fu ìŠ¤í¬ë¦½íŠ¸ ì‚¬ìš©í•˜ê¸°](./13-02-00-using-script-fu-scripts.md)ì™€ [13.3. Script-fu íŠœí† ë¦¬ì–¼](./13-03-00-a-script-fu-tutorial.md)ë¥¼ ì°¸ê³ í•´ì£¼ì„¸ìš”.

```
;  GLOWING
;  Create a text effect that simulates a glowing hot logo

(define (apply-glowing-logo-effect img
                                   logo-layer
                                   size
                                   bg-color)
  (let* (
        (grow (/ size 4))
        (feather1 (/ size 3))
        (feather2 (/ size 7))
        (feather3 (/ size 10))
        (width (car (gimp-drawable-width logo-layer)))
        (height (car (gimp-drawable-height logo-layer)))
        (posx (- (car (gimp-drawable-offsets logo-layer))))
        (posy (- (cadr (gimp-drawable-offsets logo-layer))))
        (glow-layer (car (gimp-layer-copy logo-layer TRUE)))
        (bg-layer (car (gimp-layer-new img width height RGB-IMAGE "Background" 100 NORMAL-MODE)))
        )

    (gimp-context-push)
    (gimp-context-set-defaults)

    (script-fu-util-image-resize-from-layer img logo-layer)
    (script-fu-util-image-add-layers img glow-layer bg-layer)
    (gimp-layer-translate glow-layer posx posy)

    (gimp-selection-none img)
    (gimp-context-set-background bg-color)
    (gimp-edit-fill bg-layer BACKGROUND-FILL)

    (gimp-layer-set-lock-alpha logo-layer TRUE)
    (gimp-context-set-background '(0 0 0))
    (gimp-edit-fill logo-layer BACKGROUND-FILL)

    (gimp-image-select-item img CHANNEL-OP-REPLACE logo-layer)
    (gimp-selection-feather img feather1)
    (gimp-context-set-background '(221 0 0))
    (gimp-edit-fill glow-layer BACKGROUND-FILL)
    (gimp-edit-fill glow-layer BACKGROUND-FILL)
    (gimp-edit-fill glow-layer BACKGROUND-FILL)

    (gimp-image-select-item img CHANNEL-OP-REPLACE logo-layer)
    (gimp-selection-feather img feather2)
    (gimp-context-set-background '(232 217 18))
    (gimp-edit-fill glow-layer BACKGROUND-FILL)
    (gimp-edit-fill glow-layer BACKGROUND-FILL)

    (gimp-image-select-item img CHANNEL-OP-REPLACE logo-layer)
    (gimp-selection-feather img feather3)
    (gimp-context-set-background '(255 255 255))
    (gimp-edit-fill glow-layer BACKGROUND-FILL)
    (gimp-selection-none img)

    (gimp-layer-set-mode logo-layer OVERLAY-MODE)
    (gimp-item-set-name glow-layer "Glow Layer")

    (gimp-context-pop)
  )
)


(define (script-fu-glowing-logo-alpha img
                                      logo-layer
                                      size
                                      bg-color)
  (begin
    (gimp-image-undo-group-start img)
    (apply-glowing-logo-effect img logo-layer (* size 3) bg-color)
    (gimp-image-undo-group-end img)
    (gimp-displays-flush)
  )
)

(script-fu-register "script-fu-glowing-logo-alpha"
  _"Glo_wing Hot..."
  _"Add a glowing hot metal effect to the selected region (or alpha)"
  "Spencer Kimball"
  "Spencer Kimball"
  "1997"
  "RGBA"
  SF-IMAGE      "Image"                 0
  SF-DRAWABLE   "Drawable"              0
  SF-ADJUSTMENT _"Effect size (pixels)" '(50 1 500 1 10 0 1)
  SF-COLOR      _"Background color"     '(7 0 20)
)

(script-fu-menu-register "script-fu-glowing-logo-alpha"
                         "<Image>/Filters/Alpha to Logo")


(define (script-fu-glowing-logo text
                                size
                                font
                                bg-color)
  (let* (
        (img (car (gimp-image-new 256 256 RGB)))
        (border (/ size 4))
        (text-layer (car (gimp-text-fontname img -1 0 0 text border TRUE size PIXELS font)))
        )
    (gimp-image-undo-disable img)
    (apply-glowing-logo-effect img text-layer size bg-color)
    (gimp-image-undo-enable img)
    (gimp-display-new img)
  )
)

(script-fu-register "script-fu-glowing-logo"
  _"Glo_wing Hot..."
  _"Create a logo that looks like glowing hot metal"
  "Spencer Kimball"
  "Spencer Kimball"
  "1997"
  ""
  SF-STRING     _"Text"               "GLOWING"
  SF-ADJUSTMENT _"Font size (pixels)" '(150 2 1000 1 10 0 1)
  SF-FONT       _"Font"               "Slogan"
  SF-COLOR      _"Background color"   '(7 0 20)
)

(script-fu-menu-register "script-fu-glowing-logo"
                         "<Image>/File/Create/Logos")
```

***

## ë‹¤ë¥¸ í˜ì´ì§€ë¡œ ê°€ê¸°
[â¡ï¸ ë‹¤ìŒ: 13.4.8. Imigre-26](./13-04-08-Imigre_26.md)

[â¬…ï¸ ì´ì „: 13.4.6. Chrome](./13-04-06-chrome_logo.md)

[â¬†ï¸ ìœ„: 13.4. ìŠ¤í¬ë¦½íŠ¸ ì˜ˆì œ](./13-04-00-script_examples.md)

[â¬†ï¸ ìœ„: 13. ìŠ¤í¬ë¦½íŒ…](./13-00-scripting.md)

[ğŸ  í™ˆ](./00-home.md)

[ì›ë¬¸](https://docs.gimp.org/2.10/ko/gimp-using-text.html#idm7428)
