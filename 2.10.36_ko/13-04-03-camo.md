# 13.4.3. Camouflage

## 1. `íŒŒì¼` â†’ `ë§Œë“¤ê¸°` â†’ `ë¬´ëŠ¬` â†’ `Camouflage...`

#### ê·¸ë¦¼ 13.4.3.a101. ê²°ê³¼ ì´ë¯¸ì§€
![13-04-03-a101-result(windows)(ko)](https://github.com/wonder13662/gimp/assets/15767104/d89741c7-bb24-49b8-8bf2-7dbfcbbdeb91)

#### ê·¸ë¦¼ 13.4.3.a102. ë©”ë‰´ ìœ„ì¹˜
![13-04-03-a102-menu(windows)(ko)](https://github.com/wonder13662/gimp/assets/15767104/1ee1704a-03c9-4059-900b-0cc24c631851)

#### ê·¸ë¦¼ 13.4.3.a103. ëŒ€í™”ìƒì
![13-04-03-a103-dialog(windows)(ko)](https://github.com/wonder13662/gimp/assets/15767104/7af38a73-2dd2-45b5-93fa-1a3d4ee5c142)

#### ì˜ìƒ 13.4.3.a104. ì „ì²´ ê³¼ì •
<video controls="controls" width="720" src="https://github.com/wonder13662/gimp/assets/15767104/89ce24f2-797a-42e1-a1e3-972eb79e4949"></video>

## 2. ìŠ¤í¬ë¦½íŠ¸
ì´ ìŠ¤í¬ë¦½íŠ¸ì˜ ì½”ë“œëŠ” ì•„ë˜ì™€ ê°™ìŠµë‹ˆë‹¤. ìŠ¤í¬ë¦½íŠ¸ë¥¼ ìˆ˜ì •í•´ì„œ ì‚¬ìš©í•˜ê³  ì‹¶ë‹¤ë©´, [13.2. Script-fu ìŠ¤í¬ë¦½íŠ¸ ì‚¬ìš©í•˜ê¸°](./13-02-00-using-script-fu-scripts.md)ì™€ [13.3. Script-fu íŠœí† ë¦¬ì–¼](./13-03-00-a-script-fu-tutorial.md)ë¥¼ ì°¸ê³ í•´ì£¼ì„¸ìš”.

```
(define (script-fu-camo-pattern inSize inGrain inColor1 inColor2 inColor3 inSmooth inFlatten)

  (let* (
        (theWidth inSize)
        (theHeight inSize)
        (theImage (car (gimp-image-new theWidth theHeight RGB)))
        (baseLayer (car (gimp-layer-new theImage theWidth theHeight RGBA-IMAGE "Background" 100 NORMAL-MODE)))
        (thickLayer 0)
        (thinLayer 0)
        (theBlur 0)
        )

    (gimp-context-push)
    (gimp-context-set-defaults)

    (gimp-image-insert-layer theImage baseLayer 0 0)

    (set! thickLayer (car (gimp-layer-new theImage theWidth theHeight RGBA-IMAGE "Camo Thick Layer" 100 NORMAL-MODE)))
    (gimp-image-insert-layer theImage thickLayer 0 0)

    (set! thinLayer (car (gimp-layer-new theImage theWidth theHeight RGBA-IMAGE "Camo Thin Layer" 100 NORMAL-MODE)))
    (gimp-image-insert-layer theImage thinLayer 0 0)

    (gimp-selection-all theImage)
    (gimp-context-set-background inColor1)
    (gimp-drawable-fill baseLayer BACKGROUND-FILL)

    (plug-in-solid-noise RUN-NONINTERACTIVE
        theImage thickLayer 1 0 (rand 65536) 1 inGrain inGrain)
    (plug-in-solid-noise RUN-NONINTERACTIVE
        theImage thinLayer 1 0 (rand 65536) 1 inGrain inGrain)
    (gimp-threshold thickLayer 127 255)
    (gimp-threshold thinLayer 145 255)

    (set! theBlur (- 16 inGrain))

    (gimp-context-set-background inColor2)
    (gimp-image-select-color theImage CHANNEL-OP-REPLACE thickLayer '(0 0 0))
    (gimp-edit-clear thickLayer)
    (gimp-selection-invert theImage)
    (gimp-edit-fill thickLayer BACKGROUND-FILL)
    (gimp-selection-none theImage)
    (if (= inSmooth TRUE)
        (script-fu-tile-blur theImage thickLayer theBlur TRUE TRUE FALSE)
    )


    (gimp-context-set-background inColor3)
    (gimp-image-select-color theImage CHANNEL-OP-REPLACE thinLayer '(0 0 0))
    (gimp-edit-clear thinLayer)
    (gimp-selection-invert theImage)
    (gimp-edit-fill thinLayer BACKGROUND-FILL)
    (gimp-selection-none theImage)
    (if (= inSmooth TRUE)
        (script-fu-tile-blur theImage thinLayer (/ theBlur 2) TRUE TRUE FALSE)
    )


    (if (= inFlatten TRUE)
        (gimp-image-flatten theImage)
    )

    (gimp-display-new theImage)

    (gimp-context-pop)
  )
)


; Register the function with GIMP:

(script-fu-register
  "script-fu-camo-pattern"
  _"_Camouflage..."
  _"Create an image filled with a camouflage pattern"
  "Chris Gutteridge: cjg@ecs.soton.ac.uk"
  "28th April 1998"
  "Chris Gutteridge / ECS @ University of Southampton, England"
  ""
  SF-ADJUSTMENT _"Image size"    '(256 10 1000 1 10 0 1)
  SF-ADJUSTMENT _"Granularity"   '(7 0 15 1 1 0 0)
  SF-COLOR      _"Color 1"       '(33 100 58)
  SF-COLOR      _"Color 2"       '(170 170 60)
  SF-COLOR      _"Color 3"       '(150 115 100)
  SF-TOGGLE     _"Smooth"        FALSE
  SF-TOGGLE     _"Flatten image" TRUE
)


(script-fu-menu-register "script-fu-camo-pattern"
                         "<Image>/File/Create/Patterns")

```

***

## ë‹¤ë¥¸ í˜ì´ì§€ë¡œ ê°€ê¸°
[â¡ï¸ ë‹¤ìŒ: ?]()

[â¬…ï¸ ì´ì „: ?]()

[â¬†ï¸ ìœ„: 13.4. ìŠ¤í¬ë¦½íŠ¸ ì˜ˆì œ](./13-04-00-examples.md)

[â¬†ï¸ ìœ„: 13. ìŠ¤í¬ë¦½íŒ…](./13-00-scripting.md)

[ğŸ  í™ˆ](./00-home.md)

[ì›ë¬¸](https://docs.gimp.org/2.10/ko/gimp-using-text.html#idm7428)
