# 13.4.4. Chalk

## 1. `파일` → `만들기` → `무늬` → `Chalk...`

#### 그림 13.4.4.a101. 결과 이미지
![13-04-04-a101-result(windows)(ko)](https://github.com/wonder13662/gimp/assets/15767104/cae8a1c0-580f-4772-a09e-82c97f646cd0)

#### 그림 13.4.4.a102. 메뉴 위치
![13-04-04-a102-menu(windows)(ko)](https://github.com/wonder13662/gimp/assets/15767104/a57d9f20-0228-4f6d-abda-c7b5537f1994)

#### 그림 13.4.4.a103. 대화상자
![13-04-04-a103-dialog(windows)(ko)](https://github.com/wonder13662/gimp/assets/15767104/579d65f0-a213-4bc2-b595-8c33cb181d29)

#### 영상 13.4.4.a104. 전체 과정
<video controls="controls" width="720" src="https://github.com/wonder13662/gimp/assets/15767104/8394a2d7-80f8-48fc-875f-df63a5477610"></video>

## 2. `필터` → `알파를 로고로` → `Chalk...`

#### 그림 13.4.4.a111. 원본 이미지
![13-04-04-a111-source](https://github.com/wonder13662/gimp/assets/15767104/1ff7c624-089f-4d7e-bc72-5f9111416e31)

#### 그림 13.4.4.a112. 결과 이미지
![13-04-04-a112-result](https://github.com/wonder13662/gimp/assets/15767104/07cf3537-4ea4-427d-9e5c-75c194a8b09f)

#### 그림 13.4.4.a113. 메뉴 위치
![13-04-04-a113-menu](https://github.com/wonder13662/gimp/assets/15767104/78cf8a45-de66-4cbb-849c-812b381c3e64)

#### 그림 13.4.4.a114. 대화상자
![13-04-04-a114-dialog](https://github.com/wonder13662/gimp/assets/15767104/79d825af-f4d1-433d-9fc2-ac2aedf4a1fb)

#### 영상 13.4.4.a115. 전체 과정
<video controls="controls" width="720" src="https://github.com/wonder13662/gimp/assets/15767104/0a2cdbd6-a161-4cb8-8247-0f53e0a2d99f"></video>

## 3. 스크립트
이 스크립트의 코드는 아래와 같습니다. 스크립트를 수정해서 사용하고 싶다면, [13.2. Script-fu 스크립트 사용하기](./13-02-00-using-script-fu-scripts.md)와 [13.3. Script-fu 튜토리얼](./13-03-00-a-script-fu-tutorial.md)를 참고해주세요.

```
(define (apply-chalk-logo-effect img
                                 logo-layer
                                 bg-color)
  (let* (
        (width (car (gimp-drawable-width logo-layer)))
        (height (car (gimp-drawable-height logo-layer)))
        (bg-layer (car (gimp-layer-new img
                                       width height RGB-IMAGE
                                       "Background" 100 NORMAL-MODE)))
        )

    (gimp-context-push)
    (gimp-context-set-feather FALSE)

    (gimp-selection-none img)
    (script-fu-util-image-resize-from-layer img logo-layer)
    (script-fu-util-image-add-layers img bg-layer)
    (gimp-context-set-background bg-color)
    (gimp-edit-fill bg-layer BACKGROUND-FILL)

    ; the actual effect
    (gimp-layer-set-lock-alpha logo-layer FALSE)
    (plug-in-gauss-rle RUN-NONINTERACTIVE img logo-layer 2.0 1 1)
    (plug-in-spread RUN-NONINTERACTIVE img logo-layer 5.0 5.0)
    (plug-in-ripple RUN-NONINTERACTIVE img logo-layer 27 2 0 0 0 TRUE TRUE)
    (plug-in-ripple RUN-NONINTERACTIVE img logo-layer 27 2 1 0 0 TRUE TRUE)

    ; sobel doesn't work on a layer with transparency, so merge layers:
    (let ((logo-layer
           (car (gimp-image-merge-down img logo-layer EXPAND-AS-NECESSARY))))
      (plug-in-sobel RUN-NONINTERACTIVE img logo-layer TRUE TRUE TRUE)
      (gimp-levels logo-layer 0 0 120 3.5 0 255)

      ; work-around for sobel edge detect screw-up (why does this happen?)
      ; the top line of the image has some garbage instead of the bgcolor
      (gimp-image-select-rectangle img CHANNEL-OP-ADD 0 0 width 1)
      (gimp-edit-clear logo-layer)
      )

    (gimp-selection-none img)

    (gimp-context-pop)
  )
)


(define (script-fu-chalk-logo-alpha img
                                    logo-layer
                                    bg-color)
  (begin
    (gimp-image-undo-group-start img)
    (apply-chalk-logo-effect img logo-layer bg-color)
    (gimp-image-undo-group-end img)
    (gimp-displays-flush)
  )
)

(script-fu-register "script-fu-chalk-logo-alpha"
  _"_Chalk..."
  _"Create a chalk drawing effect for the selected region (or alpha)"
  "Manish Singh <msingh@uclink4.berkeley.edu>"
  "Manish Singh"
  "October 1997"
  "RGBA"
  SF-IMAGE     "Image"            0
  SF-DRAWABLE  "Drawable"         0
  SF-COLOR    _"Background color" "black"
)

(script-fu-menu-register "script-fu-chalk-logo-alpha"
                         "<Image>/Filters/Alpha to Logo")


(define (script-fu-chalk-logo text
                              size
                              font
                              bg-color
                              chalk-color)
  (let* (
        (img (car (gimp-image-new 256 256 RGB)))
        (border (/ size 4))
        (text-layer (car (gimp-text-fontname img -1 0 0 text border TRUE size PIXELS font)))
        )

    (gimp-context-push)

    (gimp-image-undo-disable img)
    (gimp-context-set-foreground chalk-color)
    (gimp-layer-set-lock-alpha text-layer TRUE)
    (gimp-edit-fill text-layer FOREGROUND-FILL)
    (apply-chalk-logo-effect img text-layer bg-color)
    (gimp-image-undo-enable img)
    (gimp-display-new img)

    (gimp-context-pop)
  )
)

(script-fu-register "script-fu-chalk-logo"
  _"_Chalk..."
  _"Create a logo resembling chalk scribbled on a blackboard"
  "Manish Singh <msingh@uclink4.berkeley.edu>"
  "Manish Singh"
  "October 1997"
  ""
  SF-STRING     _"Text"               "CHALK"
  SF-ADJUSTMENT _"Font size (pixels)" '(150 2 1000 1 10 0 1)
  SF-FONT       _"Font"               "Sans"
  SF-COLOR      _"Background color"   "black"
  SF-COLOR      _"Chalk color"        "white"
)

(script-fu-menu-register "script-fu-chalk-logo"
                         "<Image>/File/Create/Logos")
```

***

## 다른 페이지로 가기
[➡️ 다음: 13.4.5. Chip Away](./13-04-05-chip_away.md)

[⬅️ 이전: 13.4.3. Camouflage](./13-04-03-camo.md)

[⬆️ 위: 13.4. 스크립트 예제](./13-04-00-script_examples.md)

[⬆️ 위: 13. 스크립팅](./13-00-scripting.md)

[🏠 홈](./00-home.md)

[원문](https://docs.gimp.org/2.10/ko/gimp-using-text.html#idm7428)
